Package golite;

/* https://golang.org/ref/spec */

Helpers
	/* regular expression matching all valid 16-bit characters */
	/*any = [0..0xFFFF];*/
	all = [0..127];
 	tab   = 9;
	cr    = 13;
	lf    = 10;
	eol   = cr | lf | cr lf;
	not_eol = [all - [cr + lf]];
	not_star = [all - '*'];
	not_backslash_eol = [not_eol - '\'];
	not_star_slash = [not_star - '/'];

	quote_single = ''';
	quote_double = '"';
	quote_back = '`';

	digit_nonzero = ['1'..'9'];
	digit_decimal = ['0'..'9'];
	digit_octal = ['0'..'7'];
	digit_hex = ['0'..'9'] | ['A'..'F'] | ['a'..'f'];

	letter_lc = ['a'..'z'];
	letter_uc = ['A'..'Z'];
	letter  = letter_lc | letter_uc | '_';

	char_escape = '\' ( 'a' | 'b' | 'f' | 'n' | 'r' | 't' | 'v' | '\' );
	char_escape_sq = '\' ''';
	char_escape_dq = '\"';

States
	normal,
	rune_literal,
	interpreted_string_literal,
	raw_string_literal;

Tokens
	// Rune literals
	{normal->rune_literal, rune_literal->normal} quote_single = quote_single; // '
	{rune_literal} regular_rune_character = [not_backslash_eol - quote_single];
	{rune_literal} char_escape_sq = char_escape_sq;

	// Interpreted string literals
	{normal->interpreted_string_literal, interpreted_string_literal->normal} quote_double = quote_double;
	{interpreted_string_literal} interpreted_string_chars = [not_backslash_eol - quote_double]+;
	{interpreted_string_literal} char_escape_dq = char_escape_dq;

	// Rune or interpreted string literals
	{rune_literal,interpreted_string_literal} char_escape = char_escape;

	// Raw string literals
	{normal->raw_string_literal, raw_string_literal->normal} quote_back = quote_back;
	{raw_string_literal} raw_string_contents = [all - quote_back]*;

	{normal} whitespace = (' ' | tab)+;
	{normal} eol = eol;

	/* Keywords */
	{normal} import = 'import';
	{normal} package = 'package';
	{normal} chan = 'chan';
	{normal} const = 'const';
	{normal} default = 'default';
	{normal} defer = 'defer';
	{normal} var = 'var';
	{normal} type = 'type';
	{normal} struct = 'struct';
	{normal} interface = 'interface';
	{normal} func = 'func';
	{normal} return = 'return';
	{normal} if = 'if';
	{normal} else = 'else';
	{normal} switch = 'switch';
	{normal} case = 'case';
	{normal} goto = 'goto';
	{normal} for = 'for';
	{normal} break = 'break';
	{normal} continue = 'continue';
	{normal} fallthrough = 'fallthrough';
	{normal} go = 'go';
	{normal} map = 'map';
	{normal} range = 'range';
	{normal} select = 'select';
	{normal} print = 'print';
	{normal} append = 'append';
	{normal} println = 'println';
	{normal} bool = 'bool';
	{normal} int = 'int';
	{normal} float64 = 'float64';
	{normal} rune = 'rune';
	{normal} string = 'string';

	/* Operators and Delimiters
	 * in order from the specs (easier to spot a missing one) */
	{normal} plus = '+';
	{normal} minus = '-';
	{normal} star = '*';
	{normal} slash = '/';
	{normal} percent = '%';
	{normal} amp = '&';
	{normal} pipe = '|';
	{normal} caret = '^';
	{normal} lshift = '<<';
	{normal} rshift = '>>';
	{normal} amp_caret = '&^';
	{normal} plus_eq = '+=';
	{normal} minus_eq = '-=';
	{normal} star_eq = '*=';
	{normal} slash_eq = '/=';
	{normal} percent_eq = '%=';
	{normal} amp_eq = '&=';
	{normal} pipe_eq = '|=';
	{normal} caret_eq = '^=';
	{normal} lshift_eq = '<<=';
	{normal} rshift_eq = '>>=';
	{normal} amp_caret_eq = '&^=';
	{normal} amp_amp = '&&';
	{normal} bar_bar = '||';
	{normal} larrow = '<-';
	{normal} plus_plus = '++';
	{normal} minus_minus = '--';
	{normal} eq_eq = '==';
	{normal} lt = '<';
	{normal} gt = '>';
	{normal} eq = '=';
	{normal} exclamation = '!';
	{normal} not_eq = '!=';
	{normal} lt_eq = '<=';
	{normal} gt_eq = '>=';
	{normal} colon_eq = ':=';
	{normal} elipsis = '...';
	{normal} paren_l = '(';
	{normal} paren_r = ')';
	{normal} square_l = '[';
	{normal} square_r = ']';
	{normal} brace_l = '{';
	{normal} brace_r = '}';
	{normal} comma = ',';
	{normal} dot = '.';
	{normal} semicolon = ';';
	{normal} colon = ':';

	/* Comments
	 * Single line comments go up to the EOL, but don't include the EOL token
	 * because it needs to be separate for semicolon insertion */
	{normal} comment = ('//' not_eol*)
				| ('/*' not_star* '*'+ (not_star_slash not_star* '*'+)* '/');

	/* Literals */
	{normal} lit_int  = '0' | digit_nonzero digit_decimal*;
	{normal} lit_octal = '0' digit_octal+; /* + because a single 0 should be tagged as int */
	{normal} lit_hex = '0x' digit_hex+;
	{normal} lit_float = ('0' | digit_nonzero digit_decimal*)? '.' digit_decimal*;

	/* Identifiers*/
	{normal} id = letter (letter | digit_decimal)*;

Ignored Tokens
	/* Changes here probably also require changes in GoLexer.java */
	whitespace, eol, comment;

Productions
	program = package_dec declaration*
		{-> New program(package_dec.id, [declaration])};

	package_dec {-> id} = package id semicolon {-> id};

	declaration {-> declaration} =
		  {variable} var variable_specs semicolon
			{-> New declaration.variable([variable_specs.variable_spec])}
		| {type} type type_specs semicolon
			{-> New declaration.type([type_specs.type_spec])}
		| {function} func id paren_l func_param_list paren_r type_exp? block semicolon
			{-> New declaration.function(id, [func_param_list.func_param], type_exp, [block.stm])}
	;

	/* Variable declarations */
	variable_specs {-> variable_spec*} =
		  {single_spec} variable_spec
			{-> [variable_spec]}
		| {multi_spec} paren_l variable_spec_with_semicolon* paren_r
			{-> [variable_spec_with_semicolon.variable_spec]}
	;

	variable_spec_with_semicolon {-> variable_spec} =
		variable_spec semicolon {-> variable_spec};

	variable_spec =
		  {no_expr} id_list type_exp
			{-> New variable_spec.typed([id_list.id], type_exp, [])}
		| {with_expr} id_list type_exp eq exp_list
			{-> New variable_spec.typed([id_list.id], type_exp, [exp_list.exp])}
		| {with_expr_no_type} id_list eq exp_list
			{-> New variable_spec.untyped([id_list.id], [exp_list.exp])}
	;

	id_list {-> id*} = id id_list_tail* {-> [id, id_list_tail.id]};

	id_list_tail {-> id} = comma id {-> id};

	exp_list {-> exp*} = exp exp_list_tail* {-> [exp, exp_list_tail.exp]};

	exp_list_tail {-> exp} = comma exp {-> exp};

	/* Type declarations */
	type_specs {-> type_spec*} =
		  {single_spec} type_spec
			{-> [type_spec]}
		| {multi_spec} paren_l type_spec_with_semicolon* paren_r
			{-> [type_spec_with_semicolon.type_spec]}
	;

	type_spec_with_semicolon {-> type_spec} = type_spec semicolon {-> type_spec};

	type_spec = id type_exp;

	/* Function declarations */
	func_param_list {-> func_param*} =
		  {nonempty} func_param func_param_list_tail*
				{-> [func_param, func_param_list_tail.func_param]}
		| {empty} {-> []}
	;

	func_param = id_list type_exp {-> New func_param([id_list.id], type_exp)};

	func_param_list_tail {-> func_param} = comma func_param {-> func_param};

	variable_spec_list_tail {-> variable_spec} =
		comma variable_spec {-> variable_spec};

	/* Types */
	type_exp =
		  {base} base_type
			{-> base_type.type_exp}
		| {alias} id
			{-> New type_exp.alias(id)}
		| {struct} struct brace_l field_dec* brace_r
			{-> New type_exp.struct([field_dec])}
		| {slice} square_l square_r type_exp
			{-> New type_exp.slice(type_exp)}
		| {array} square_l integer square_r type_exp
			{-> New type_exp.array(integer, type_exp)}
	;

	base_type {-> type_exp} =
		  {int} int
			{-> New type_exp.int()}
		| {float64} float64
			{-> New type_exp.float64()}
		| {bool} bool
			{-> New type_exp.bool()}
		| {rune} rune
			{-> New type_exp.rune()}
		| {string} string
			{-> New type_exp.string()}
	;

	field_dec = id_list type_exp semicolon
		{-> New field_dec([id_list.id], type_exp)};

	/* Statements */
	stm {->stm?} =
		  {simple_form} simple_stm {-> simple_stm.stm}
		| {variable_dec} var variable_specs semicolon
			{-> New stm.variable_dec([variable_specs.variable_spec])}
		| {type_dec} type type_specs semicolon
			{-> New stm.type_dec([type_specs.type_spec])}
		| {print} print_op paren_l exp_list_star paren_r semicolon
			{-> New stm.print(print_op, [exp_list_star.exp])}
		| {return} return exp? semicolon {-> New stm.return(exp)}
		| {block} block semicolon {-> New stm.block([block.stm])}
		| {if} if_stm {-> if_stm.stm}
		| {switch} switch simple_stm? exp? brace_l switch_clause* brace_r
			{-> New stm.switch(simple_stm.stm, exp, [switch_clause])}
		| {for} for_stm {-> for_stm.stm}
		| {break} break semicolon {-> New stm.break()}
		| {continue} continue semicolon {-> New stm.continue()}
	;

	simple_stm {-> stm?} = bare_simple_stm semicolon {-> bare_simple_stm.stm};

	bare_simple_stm {-> stm?} =
		  {empty} {-> Null}
		| {expression} exp {-> New stm.expression(exp)}
		/* Ensure the expressions are lvalues in weeding */
		| {assign} [lvalue_list]:exp_list eq exp_list
			{-> New stm.assign([lvalue_list.exp], [exp_list.exp])}
		| {op_assign} primary_exp assign_op exp
			{-> New stm.op_assign(primary_exp.exp, assign_op, exp)}
		/* Ensure the expressions are id's in weeding */
		| {short_var_dec} [id_list]:exp_list colon_eq exp_list
			{-> New stm.short_variable_dec([id_list.exp], [exp_list.exp])}
		/* Ensure the expressions are lvalues in weeding */
		| {inc_dec} primary_exp postfix_op
			{-> New stm.inc_dec(primary_exp.exp, postfix_op)}
	;

	if_stm {->stm} =
		  {if} if simple_stm? exp block
			{-> New stm.if(simple_stm.stm, exp, [block.stm], [])}
		| {if_else} if simple_stm? exp [if_block]:block else [else_block]:block
			{-> New stm.if(simple_stm.stm, exp, [if_block.stm], [else_block.stm])}
		| {if_else_if} if simple_stm? exp block else if_stm
			{-> New stm.if(simple_stm.stm, exp, [block.stm], [if_stm.stm])}
	;

	switch_clause =
		  {expression} case exp_list colon stm* fallthrough_stm?
			{-> New switch_clause.conditional([exp_list.exp], [stm], fallthrough_stm)}
		| {default} default colon stm* fallthrough_stm?
			{-> New switch_clause.default([stm], fallthrough_stm)}
	;

	fallthrough_stm = fallthrough semicolon {-> New fallthrough_stm()};

	for_stm {->stm} =
		  {while_loop} for exp? block
			{-> New stm.for(Null, exp, Null, [block.stm])}
		| {full_loop} for [init]:simple_stm exp? semicolon [post]:bare_simple_stm block
			{-> New stm.for(init.stm, exp, post.stm, [block.stm])}
	;

	block {-> stm*} = brace_l stm* brace_r {-> [stm]};

	exp_list_star {-> exp*} =
		  {empty} {-> []}
		| {non_empty} exp_list {-> [exp_list.exp]}
	;

	print_op =
		  {simple} print {-> New print_op.simple()}
		| {line} println {-> New print_op.line()}
	;

	postfix_op =
		  {inc} plus_plus {-> New postfix_op.inc()}
		| {dec} minus_minus {-> New postfix_op.dec()}
	;

	assign_op =
		  {plus_eq} plus_eq {-> New assign_op.plus()}
		| {minus_eq} minus_eq {-> New assign_op.minus()}
		| {star_eq} star_eq {-> New assign_op.star()}
		| {slash_eq} slash_eq {-> New assign_op.slash()}
		| {percent_eq} percent_eq {-> New assign_op.percent()}
		| {amp_eq} amp_eq {-> New assign_op.amp()}
		| {pipe_eq} pipe_eq {-> New assign_op.pipe()}
		| {caret_eq} caret_eq {-> New assign_op.caret()}
		| {lshift_eq} lshift_eq {-> New assign_op.lshift()}
		| {rshift_eq} rshift_eq {-> New assign_op.rshift()}
		| {amp_caret_eq} amp_caret_eq {-> New assign_op.amp_caret()}
	;

	cmp_op {-> binary_op} =
		  {eq} eq_eq {-> New binary_op.eq()}
		| {gt} gt {-> New binary_op.gt()}
		| {lt} lt {-> New binary_op.lt()}
		| {gt_eq} gt_eq {-> New binary_op.gt_eq()}
		| {lt_eq} lt_eq {-> New binary_op.lt_eq()}
		| {not_eq} not_eq {-> New binary_op.not_eq()}
	;

	term_op {-> binary_op} =
		  {plus} plus {-> New binary_op.plus()}
		| {minus} minus {-> New binary_op.minus()}
		| {pipe} pipe {-> New binary_op.pipe()}
		| {caret} caret {-> New binary_op.caret()}
	;

	factor_op {-> binary_op} =
		  {star} star {-> New binary_op.star()}
		| {slash} slash {-> New binary_op.slash()}
		| {percent} percent {-> New binary_op.percent()}
		| {lshift} lshift {-> New binary_op.lshift()}
		| {rshift} rshift {-> New binary_op.rshift()}
		| {amp} amp {-> New binary_op.amp()}
		| {amp_caret} amp_caret {-> New binary_op.amp_caret()}
	;

	unary_op =
		  {plus} plus {-> New unary_op.plus()}
		| {minus} minus {-> New unary_op.minus()}
		| {exclamation} exclamation {-> New unary_op.exclamation()}
		| {caret} caret {-> New unary_op.caret()}
	;

	/* Expressions */
	exp =
		  {logical_or} exp bar_bar and_exp {-> New exp.binary(exp, New binary_op.logical_or(), and_exp.exp)}
		| {base} and_exp {-> and_exp.exp}
	;

	and_exp {-> exp} =
		  {logical_and} and_exp amp_amp comparison_exp {-> New exp.binary(and_exp.exp, New binary_op.logical_and(), comparison_exp.exp)}
		| {base} comparison_exp {-> comparison_exp.exp}
	;

	comparison_exp {-> exp} =
		  {comparison} comparison_exp cmp_op term {-> New exp.binary(comparison_exp.exp, cmp_op.binary_op, term.exp)}
		| {base} term {-> term.exp}
	;

	term {-> exp} =
		  {term} term term_op factor {-> New exp.binary(term.exp, term_op.binary_op, factor.exp)}
		| {base} factor {-> factor.exp}
	;

	factor {-> exp} =
		  {factor} factor factor_op unary_exp {-> New exp.binary(factor.exp, factor_op.binary_op, unary_exp.exp)}
		| {base} unary_exp {-> unary_exp.exp}
	;

	unary_exp {-> exp} =
		  {unary} unary_op unary_exp {-> New exp.unary(unary_op, unary_exp.exp)}
		| {primary} primary_exp {-> primary_exp.exp}
	;

	primary_exp {-> exp} =
		  {recurse} paren_l exp paren_r
			{-> exp}
		| {variable} id
			{-> New exp.variable(id)}
		| {array_access} primary_exp square_l exp square_r
			{-> New exp.array_access(primary_exp.exp, exp)}
		| {field_access} primary_exp dot id
			{-> New exp.field_access(primary_exp.exp, id)}
		| {integer} integer
			{-> New exp.integer(integer)}
		| {float} lit_float
			{-> New exp.float(lit_float)}
		| {rune} [l]:quote_single rune_character [r]:quote_single
			{-> New exp.rune(rune_character)}
		| {string} string_literal
			{-> New exp.string(string_literal.string_literal)}
		| {function_call} primary_exp paren_l exp_list_star paren_r
			{-> New exp.function_call(primary_exp.exp, [exp_list_star.exp])}
		| {base_type_cast} base_type paren_l exp paren_r
			{-> New exp.base_type_cast(base_type.type_exp, exp)}
		| {append} append paren_l id comma exp paren_r
			{-> New exp.append(id, exp)}
	;

	integer {-> integer} =
		  {lit_int} lit_int
		| {lit_hex} lit_hex
		| {lit_octal} lit_octal
	;

	rune_character =
		  {regular} regular_rune_character
		| {escape} char_escape
		| {sq} char_escape_sq
	;

	string_literal =
		  {interpreted} [l]:quote_double interpreted_string_parts [r]:quote_double
			{-> New string_literal.interpreted([interpreted_string_parts.interpreted_string_part])}
		| {raw} [l]:quote_back raw_string_contents [r]:quote_back
			{-> New string_literal.raw(raw_string_contents)}
		| {empty_raw} [l]:quote_back [r]:quote_back
			{-> New string_literal.raw(Null)}
	;

	interpreted_string_parts {-> interpreted_string_part*} =
		  {empty}
			{-> []}
		| {non_empty} interpreted_string_part interpreted_string_parts
			{-> [interpreted_string_part, interpreted_string_parts.interpreted_string_part]}
	;

	interpreted_string_part =
		  {regular} interpreted_string_chars
		| {escape} char_escape
		| {dq} char_escape_dq
	;

Abstract Syntax Tree
	program = [package_name]:id [declarations]:declaration*;

	declaration =
		  {variable} variable_spec*
		| {type} type_spec*
		| {function} id func_param* [return_type]:type_exp? stm*
	;

	variable_spec =
		  {typed} id+ type_exp exp*
		| {untyped} id+ exp+
	;

	type_spec = id type_exp;

	func_param = id+ type_exp;

	type_exp =
		  {alias} id
		| {int}
		| {float64}
		| {bool}
		| {rune}
		| {string}
		| {struct} field_dec*
		| {slice} type_exp
		| {array} integer type_exp
	;

	field_dec = id+ type_exp;

	stm =
		  {expression} exp
		| {assign} [lvalue]:exp+ exp+
		| {op_assign} [lvalue]:exp assign_op exp
		| {inc_dec} exp postfix_op
		| {short_variable_dec} [ids]:exp+ exp+
		| {variable_dec} variable_spec+
		| {type_dec} type_spec+
		| {print} print_op exp*
		| {return} exp?
		| {block} stm*
		| {if} stm? exp [if_block]:stm* [else_block]:stm*
		| {switch} stm? exp? switch_clause*
		| {for} [init]:stm? exp? [post]:stm? stm*
		| {break}
		| {continue}
	;

	switch_clause =
		  {conditional} exp+ stm* fallthrough_stm?
		| {default} stm* fallthrough_stm?
	;

	fallthrough_stm = ;

	print_op = {simple} | {line};

	postfix_op = {inc} | {dec};

	assign_op =
		  {plus}
		| {minus}
		| {star}
		| {slash}
		| {percent}
		| {amp}
		| {pipe}
		| {caret}
		| {lshift}
		| {rshift}
		| {amp_caret}
	;

	binary_op =
		  {logical_or}
		| {logical_and}
		| {eq}
		| {gt}
		| {lt}
		| {gt_eq}
		| {lt_eq}
		| {not_eq}
		| {plus}
		| {minus}
		| {star}
		| {slash}
		| {percent}
		| {amp}
		| {pipe}
		| {caret}
		| {lshift}
		| {rshift}
		| {amp_caret}
	;

	unary_op = {plus} | {minus} | {exclamation} | {caret} ;

	exp =
		  {variable} id
		| {array_access} [array]:exp [index]:exp
		| {field_access} exp id
		| {integer} integer
		| {float} lit_float
		| {rune} rune_character
		| {string} string_literal
		| {function_call} [functor]:exp exp*
		| {base_type_cast} type_exp exp
		| {append} id exp
		| {binary} [left]:exp binary_op [right]:exp
		| {unary} unary_op exp
	;

	integer =
		  {lit_int} lit_int
		| {lit_hex} lit_hex
		| {lit_octal} lit_octal
	;

	rune_character =
		  {regular} regular_rune_character
		| {escape} char_escape
		| {sq} char_escape_sq
	;

	string_literal =
		  {interpreted} interpreted_string_part*
		| {raw} raw_string_contents?
	;

	interpreted_string_part =
		  {regular} interpreted_string_chars
		| {escape} char_escape
		| {dq} char_escape_dq
	;
