Package golite;

Helpers
	/* regular expression matching all valid 16-bit characters */
	any = [0..0xFFFF];
 	tab   = 9;
	cr    = 13;
	lf    = 10;
	eol   = cr | lf | cr lf;
	blank = (' ' | tab | eol)+;
	digit = ['0'..'9'];
	lowercase = ['a'..'z'];
	uppercase = ['A'..'Z'];
	letter  = lowercase | uppercase;
	idletter = letter | '_';
	idchar  = letter | '_' | digit;

Tokens
	/* Characters */
	blank = blank;
	pound = '#';
	ampersand = '&';
	vertbar = '|';
	hat   = '^';
	star  = '*';
	fslash = '/';
	bslash = '\';
	plus  = '+';
	minus = '-';
	percent = '%';
	equal = '=';
	colon = ':';
	semicolon = ';';
	comma = ',';
	anglel = '<';
	angler = '>';
	parenl = '(';
	parenr = ')';
	squarel = '[';
	squarer = ']';
	curlyl = '{';
	curlyr = '}';

	int_lit  = '0' | [digit-'0'] digit*;
	float_lit = ('0' | [digit-'0'] digit*) '.' digit+;

	/* Keywords */
	import = 'import';
	package = 'package';
	chan = 'chan';
	const = 'const';
	default = 'default';
	defer = 'defer';
	var = 'var';
	type = 'type';
	struct = 'struct';
	interface = 'interface';
	func = 'func';
	return = 'return';
	if = 'if';
	else = 'else';
	switch = 'switch';
	case = 'case';
	goto = 'goto';
	for = 'for';
	break = 'break';
	continue = 'continue';
	fallthrough = 'fallthrough';
	go = 'go';
	map = 'map';
	range = 'range';
	select = 'select';
	print = 'print';
	append = 'append';
	println = 'println';
	bool = 'bool';
	int = 'int';
	float64 = 'float64';
	rune = 'rune';
	string = 'string';

	/* Comments */

	commentl = '#' [any-lf]* eol;
	commentb= '/*' any* '*/';

	/* Literals */

	/* Identifiers*/
	id = idletter idchar*;


Ignored Tokens
	blank, commentl, commentb;

Productions
	program = package_dec declaration* {-> New program(package_dec.id, [declaration])};

	package_dec {-> id} = package id semicolon {-> id};

	declaration =
		  {variable} var variable_specs semicolon {-> New declaration.variable([variable_specs.variable_spec])}
		| {type}     type type_specs semicolon    {-> New declaration.type([type_specs.type_spec])}
	;

	/* Variable declarations */
	variable_specs {-> variable_spec*} =
				  {single_spec} variable_spec {-> [variable_spec]}
				| {multi_spec} parenl variable_spec_with_semicolon* parenr {-> [variable_spec_with_semicolon.variable_spec]}
	;

	variable_spec_with_semicolon {-> variable_spec} = variable_spec semicolon {-> variable_spec};

	variable_spec =
				  {no_expr} id_list type_id {-> New variable_spec.typed([id_list.id], type_id, [])}
				| {with_expr} id_list type_id equal exp_list {-> New variable_spec.typed([id_list.id], type_id, [exp_list.exp])}
				| {with_expr_no_type} id_list equal exp_list {-> New variable_spec.untyped([id_list.id], [exp_list.exp])}
	;

	id_list {-> id*} = id id_list_tail* {-> [id, id_list_tail.id]};

	id_list_tail {-> id} = comma id {-> id};

	exp_list {-> exp*} = exp exp_list_tail* {-> [exp, exp_list_tail.exp]};

	exp_list_tail {-> exp} = comma exp {-> exp};

	/* Type declarations */
	type_specs {-> type_spec*} =
		  {single_spec} type_spec {-> [type_spec]}
		| {multi_spec} parenl type_spec_with_semicolon* parenr {-> [type_spec_with_semicolon.type_spec]}
	;

	type_spec_with_semicolon {-> type_spec} = type_spec semicolon {-> type_spec};

	type_spec = id type_id;

	type_id =
		  {var} id {-> New type_id.var(id)}
		| {int} int {-> New type_id.int()}
		| {float64} float64 {-> New type_id.float64()}
		| {bool} bool {-> New type_id.bool()}
		| {rune} rune {-> New type_id.rune()}
		| {string} string {-> New type_id.string()}
	;

	exp =
		  {variable} id
		| {int_lit} int_lit
		| {float_lit} float_lit
	;

Abstract Syntax Tree
	program = [package_name]:id [declarations]:declaration*;

	declaration =
		  {variable} variable_spec*
		| {type} type_spec*
	;

	variable_spec =
		  {typed} id+ type_id exp*
		| {untyped} id+ exp+
	;

	type_spec = id type_id;

	type_id = {var} id | {int} | {float64} | {bool} | {rune} | {string};

	exp =
		  {variable} id
		| {int_lit} int_lit
		| {float_lit} float_lit
	;
